import QtQuick
import qs.config

// Win98-style bevelled box. raised by default, `sunken` for inputs/pressed.
// `hell: Theme.hell` gives it hell's obsidian bevels (Theme.realm).
Item {
    id: root

    property bool hell: false
    property color color: hell ? Theme.hellFace : Theme.face
    property int bevel: Theme.u
    property bool sunken: false
    property bool outline: true
    property bool flat: false
    property bool shadow: false
    property int shadowSize: Theme.u * 2
    property color hiColor: hell ? Theme.hellHi : Theme.hi
    property color loColor: hell ? Theme.hellLo : Theme.lo
    property color edgeColor: hell ? Theme.hellEdge : Theme.edge
    readonly property int ob: outline ? bevel : 0
    readonly property int inset: ob + (flat ? 0 : bevel)
    default property alias content: inner.data

    // hard pixel shadow (right + bottom strips so translucent boxes stay clean)
    Rectangle {
        visible: root.shadow
        x: root.width
        y: root.shadowSize
        width: root.shadowSize
        height: root.height
        color: Qt.alpha(Theme.shadow, Theme.dark ? 0.55 : 0.35)
    }
    Rectangle {
        visible: root.shadow
        x: root.shadowSize
        y: root.height
        width: root.width
        height: root.shadowSize
        color: Qt.alpha(Theme.shadow, Theme.dark ? 0.55 : 0.35)
    }

    Rectangle {
        anchors.fill: parent
        color: root.color
        border.width: root.ob
        border.color: root.edgeColor
    }
    Rectangle {
        visible: !root.flat
        x: root.ob
        y: root.ob
        width: root.width - 2 * root.ob
        height: root.bevel
        color: root.sunken ? root.loColor : root.hiColor
    }
    Rectangle {
        visible: !root.flat
        x: root.ob
        y: root.ob
        width: root.bevel
        height: root.height - 2 * root.ob
        color: root.sunken ? root.loColor : root.hiColor
    }
    Rectangle {
        visible: !root.flat
        x: root.ob + root.bevel
        y: root.height - root.ob - root.bevel
        width: root.width - 2 * root.ob - root.bevel
        height: root.bevel
        color: root.sunken ? root.hiColor : root.loColor
    }
    Rectangle {
        visible: !root.flat
        x: root.width - root.ob - root.bevel
        y: root.ob + root.bevel
        width: root.bevel
        height: root.height - 2 * root.ob - root.bevel
        color: root.sunken ? root.hiColor : root.loColor
    }

    Item {
        id: inner
        anchors.fill: parent
        anchors.margins: root.inset
    }
}
