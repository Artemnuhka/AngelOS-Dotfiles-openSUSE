pragma Singleton
pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import Quickshell.Io
import qs.config

// Shake the mouse to find the pointer, like macOS (Settings → Cursor): scripts/shake-watch.py
// spots a shake in evdev (needs the `input` group, like the Meta tap), modules/cursor/ShakeCursor
// draws the arrow of the cursor theme (scripts/cursor-image.py) bigger for a moment.
Singleton {
    id: root

    property string status: "off"      // off | starting | ready | noperm | noevdev | error
    property bool shaking: false
    // the arrow picture: {png, nominal, width, height, xhot, yhot}
    property var image: null
    readonly property bool wanted: Config.ready && Config.cursor.shake && (!Shell.dev || Quickshell.env("ANGELOS_DEV_SHAKE") === "1")
    readonly property real zoom: Math.max(1.5, Math.min(8, Config.cursor.shakeScale || 4))

    function pulse() {
        // games and fullscreen video: fast flicks are not a lost pointer
        if (Shell.locked || MetaTap.coversOutput(Niri.focusedWindow))
            return;
        shaking = true;
        calm.interval = 420;
        calm.restart();
    }
    // Settings → Cursor → "Show me": long enough to move the mouse and see it
    function demo() {
        shaking = true;
        calm.interval = 1600;
        calm.restart();
    }
    // a click while it is big: shrink at once, the next click goes through
    function dismiss() {
        calm.stop();
        shaking = false;
    }
    Timer {
        id: calm
        interval: 420
        onTriggered: root.shaking = false
    }

    Process {
        id: daemon
        running: root.wanted
        command: ["python3", "-u", Quickshell.shellDir + "/scripts/shake-watch.py", "--sensitivity", ["low", "normal", "high"].includes(Config.cursor.shakeSensitivity) ? Config.cursor.shakeSensitivity : "normal"]
        onRunningChanged: {
            if (running)
                root.status = "starting";
            else if (root.status === "starting" || root.status === "ready")
                root.status = root.wanted ? "error" : "off";
        }
        stdout: SplitParser {
            onRead: line => {
                if (line === "shake")
                    root.pulse();
                else if (["ready", "noperm", "noevdev"].includes(line))
                    root.status = line;
            }
        }
        stderr: StdioCollector {}
        onExited: if (root.wanted && root.status !== "noperm" && root.status !== "noevdev")
            restart.start()
    }
    Timer {
        id: restart
        interval: 5000
        onTriggered: if (root.wanted)
            daemon.running = true
    }
    onWantedChanged: {
        if (!wanted)
            status = "off";
        else if (!image)
            imager.load();
    }

    // the arrow of the current theme, again when the theme changes
    readonly property string theme: Cursors.theme || "default"
    onThemeChanged: if (wanted)
        imager.load()
    Component.onCompleted: if (wanted)
        imager.load()
    Process {
        id: imager
        function load() {
            running = false;
            command = ["python3", Quickshell.shellDir + "/scripts/cursor-image.py", root.theme, Config.cacheDir + "/cursor-" + root.theme.replace(/[^A-Za-z0-9_.-]/g, "_") + ".png"];
            running = true;
        }
        stdout: StdioCollector {
            onStreamFinished: {
                try {
                    const j = JSON.parse(text);
                    root.image = j.png ? j : null;
                } catch (e) {
                    root.image = null;
                }
            }
        }
        stderr: StdioCollector {}
    }
}
